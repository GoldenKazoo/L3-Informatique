# TD du 05/02/2026 - UPEC, droit et informatique

Aujourd'hui, vous êtes employé.e à la Caisse Nationale d'Allocations
Familiales, et votre mission est d'automatiser le calcul de l'Aide Personnalisée
au Logement (APL), versée chaque mois à plus de 5 millions de bénéficiaires.

Ce calcul est passablement compliqué : la [brochure du Ministère du logement](https://www.ecologie.gouv.fr/sites/default/files/documents/Brochure-bareme-2024-APL.pdf)
le détaillant fait 84 pages !

## Exercice 1 : le montant des charges

Le calcul de l'APL prend en compte le fait que le locataire paye en plus de son
loyer des charges locatives. Parce que ces charges varient en réalité énormément
en fonction des modalités collectives de l'immeuble (présence d'un gardien,
parking, etc.), le droit fixe un montant forfaitaire pour ces charges qui ne
dépend que de la taille du foyer.

Dans cet exercice, il vous est demandé de calculer ce montant forfaitaire de charges
avec les règles fixées par les articles 9 et 16 2° de l'arrêté du 27 septembre 2019.
Un début incomplet d'implémentation Catala vous est fourni ci-après, appuyez-vous
dessus pour le reste de l'exercice.

### Questions de l'exercice

1°) Complétez le code Catala afin de terminer le calcul du plafond
de loyer en prenant en compte l'ensemble des règles des articles 9 et 16.

2°) Vérifiez que votre implémentation Catala existante retourne bien un montant
forfaitaire de charges de 29,03 € sur le test suivant :

```catala
déclaration champ d'application Test11:
  résultat calcul contenu CalculMontantForfaitaireCharges

champ d'application Test11:
  définition calcul égal à
    résultat de CalculMontantForfaitaireCharges avec {
      -- situation_familiale: PersonneSeule
      -- nombre_personnes_à_charge: 0
      -- colocation: vrai
    }
```

3°) Calculez le montant forfaitaire des charges pour un couple avec 3 enfants à
charge ne vivant pas en colocation. Vérifiez que votre implémentation Catala renvoie bien le bon résultat.


### Règles de calcul du montant forfaitaire de charges et code Catala

####  Déclaration des structures de données et des champs d'application

```catala
déclaration champ d'application CalculMontantForfaitaireCharges:
  entrée nombre_personnes_à_charge contenu entier
  entrée colocation contenu booléen
  entrée situation_familiale contenu SituationFamiliale
  résultat montant_forfaitaire_charges_d823_16 contenu argent

déclaration énumération SituationFamiliale:
  -- PersonneSeule
  -- Couple
```

#### Arrêté du 27 septembre 2019 - Article 9

Les montants forfaitaires au titre des charges visés au 3° de
l'article D. 823-16 du même code sont fixés comme suit (en euros) :

----------------------------------
Désignation	          Toutes zones
--------------------- ------------
Bénéficiaire isolé ou 58,08
couple sans personne
à charge

Par personne          13,17
supplémentaire
à charge
----------------------------------

```catala
champ d'application CalculMontantForfaitaireCharges:
  étiquette article_9
  définition montant_forfaitaire_charges_d823_16
  égal à
    # À compléter
    58,08 € + nombre_personnes_à_charge * 13,17€
```

#### Arrêté du 27 septembre 2019 - Article 16

Dans le cas des colocataires prévus à l'article D. 823-18 du même code :

1°) [...]

2°) Le montant forfaitaire au titre des charges est fixé comme suit (en euros) :

Composition du foyer	           Montant
-------------------------------- -------
Bénéficiaire isolé               29,03
Couple sans personne à charge	   58,08
Majoration par personne à charge 13,17

```catala
champ d'application CalculMontantForfaitaireCharges:
  étiquette article_16 exception article_9
  définition montant_forfaitaire_charges_d823_16
  sous condition colocation ou (situation_familiale = Couple)
  conséquence égal à
    # À compléter
    29,03 € + nombre_personnes_à_charge * 13,17€
```

## Exercice 2 : le plafond de loyer

Le montant de l'APL dépend du loyer payé : en première approximation,
plus le loyer est élevé, plus on gagne d'APL. Cependant, ce loyer n'est retenu
que sous un certain plafond.

Dans cet exercice, il vous est demandé de calculer ce plafond à partir des règles
énoncées aux article 7, 8 et 16 1° de l'arrêté du 27 septembre 2019. Un début
incomplet d'implémentation Catala vous est fourni ci-après, appuyez-vous dessus
pour le reste de l'exercice.


### Questions de l'exercice

1°) Vérifiez que l'implémentation Catala existante retourne bien un plafond de loyer
égal à 239,48 € sur le test suivant :

```catala
déclaration champ d'application Test21:
  résultat calcul contenu CalculPlafondLoyer

champ d'application Test21:
  définition calcul égal à
    résultat de CalculPlafondLoyer avec {
      -- logement_est_chambre: faux
      -- âgées_ou_handicap_adultes_hébergées_onéreux_particuliers: faux
      -- zone: Zone1
      -- situation_familiale: PersonneSeule
      -- nombre_personnes_à_charge: 0
      -- colocation: vrai
    }
  assertion calcul.plafond_loyer_d823_16_2 = 239,48 €
```

2°) Complétez le code Catala afin de terminer le calcul du plafond
de loyer en prenant en compte l'ensemble des règles des articles 7, 8 et 16.


3°) Calculez le montant du plafond de loyer pour un couple avec 3 enfants à
charge en zone 2 (le couple ne vit pas dans une chambre, ni en colocation,
et nous ne sommes pas dans le cas de personnes âgées ou d'adultes handicapés).
Vérifiez que votre implémentation Catala renvoie bien le bon résultat.


4°) Que se passe-t-il lorsqu'une personne vit à la fois dans une chambre
et une colocation ? Écrivez un cas de test pour cette situation, et observez
le comportement de votre code Catala. Est-il adapté ? D'après vous, qu'est-ce
que l'implémentation devrait retourner dans ce cas ? Appelez un chargé de TD
et donnez-lui votre réponse à cette question quand vous l'avez formulée.

### Règles de calcul du plafond de loyer et code Catala

####  Déclaration des structures de données et des champs d'application

```catala
déclaration champ d'application CalculPlafondLoyer:
  entrée nombre_personnes_à_charge contenu entier
  entrée situation_familiale contenu SituationFamiliale
  entrée zone contenu ZoneDHabitation
  entrée logement_est_chambre contenu booléen
  entrée âgées_ou_handicap_adultes_hébergées_onéreux_particuliers
    contenu booléen
  entrée colocation contenu booléen

  résultat plafond_loyer_d823_16_2 contenu argent

déclaration énumération ZoneDHabitation:
  -- Zone1
  -- Zone2
  -- Zone3
```

#### Arrêté du 27 septembre 2019 - Article 7

Les plafonds de loyers visés au 2° de l'article D. 823-16 du même code sont fixés comme suit (en euros) :

---------------------------------------------------------------------------------------------------
Zone Personne seule Couple sans personne à charge Personne seule ou couple    Par personne à
                                                  ayant une personne à charge charge supplémentaire
---- -------------- ----------------------------- --------------------------- ---------------------
I	   319,30         385,09                        435,24	                    63,15

II   278,28         340,62                        383,28                      55,79

III  260,82         316,17                        354,51                      50,81
---------------------------------------------------------------------------------------------------

```catala
champ d'application CalculPlafondLoyer:

  # Colonne 1
  étiquette base
  définition plafond_loyer_d823_16_2
  sous condition
    (situation_familiale sous forme PersonneSeule)
    et nombre_personnes_à_charge = 0
  conséquence égal à
    selon zone sous forme
    -- Zone1 : 319,30€
    -- Zone2 : 278,28€
    -- Zone3 : 260,82€

  # Colonne 2
  étiquette base
  définition plafond_loyer_d823_16_2
  sous condition
    (situation_familiale sous forme Couple)
    et nombre_personnes_à_charge = 0
  conséquence égal à
    selon zone sous forme
    -- Zone1 : 385,09€
    -- Zone2 : 340,62€
    -- Zone3 : 316,17€

  étiquette base
  définition plafond_loyer_d823_16_2
  # Colonne 3 
  sous condition
    ((situation_familiale sous forme PersonneSeule)
    ou (situation_familiale sous forme Couple))
    et nombre_personnes_à_charge > 0
  conséquence égal à
    selon zone sous forme
    -- Zone1 : 435,24€
    -- Zone2 : 383,28€
    -- Zone3 : 354,51€

  exception base
  définition plafond_loyer_d823_16_2
  # Colonne 4
  sous condition
    nombre_personnes_à_charge > 1
  conséquence égal à
    selon zone sous forme
    -- Zone1 : 63,15€ * (nombre_personnes_à_charge - 1) + 435,24€  
    -- Zone2 : 55,79€ * (nombre_personnes_à_charge - 1) + 383,28€  
    -- Zone3 : 50,81€ * (nombre_personnes_à_charge - 1) + 354,51€  


```
#### Arrêté du 27 septembre 2019 - Article 8

Dans le cas où le logement occupé est une chambre, les plafonds
de loyers visés au 2° de l'article D. 823-16 du même code sont
fixés comme suit, quelle que soit la taille de la famille (en euros) :

- 90 % des loyers plafonds de location pour une personne isolée ;
- 75 % des loyers plafonds de location pour une personne isolée,
dans le cas des personnes âgées ou handicapées adultes hébergées
à titre onéreux chez des particuliers.

On obtient les loyers plafonds suivants (en euros) :

MONTANTS DES LOYERS PLAFONDS CHAMBRE EN APL ET EN AL
(arrondis au centime d'euros le plus proche)

----------------------------------------
Bénéficiaires	            Zones	 Montants
------------------------- ------ --------
Cas général	              I	     287,37

                          II	   250,45

                          III    234,74


Cas des personnes âgées   I      239,48
ou handicapées adultes
hébergées à titre onéreux
chez des particuliers

                          II     208,71

                          III    195,62
----------------------------------------

```catala
champ d'application CalculPlafondLoyer:

  étiquette base
  définition plafond_loyer_d823_16_2
  sous condition logement_est_chambre
  conséquence égal à
    selon zone sous forme
    -- Zone1 : 287,37€
    -- Zone2 : 250,45€
    -- Zone3 : 234,74€

  étiquette base
  définition plafond_loyer_d823_16_2
  sous condition
    logement_est_chambre
    et âgées_ou_handicap_adultes_hébergées_onéreux_particuliers
  conséquence égal à
    selon zone sous forme
    -- Zone1 : 239,48€
    -- Zone2 : 208,71€
    -- Zone3 : 195,62€
```

#### Arrêté du 27 septembre 2019 - Article 16

Dans le cas des colocataires prévus à l'article D. 823-18 du même code :

1° Les plafonds de loyers sont fixés à 75 % des plafonds de loyers définis au 2° de l'article
D. 823-16 du même code et fixés à l'article 7.

Les montants obtenus par l'application de ces pourcentages sont arrondis au centime d'euro le plus proche.
On obtient les loyers plafonds suivants (en euros) :

--------------------------------------------------------
Zone	                             I       II     III
---------------------------------- ------- ------ ------
Personne seule	                    239,48 208,71	195,62

Couple sans personne à charge	      288,82 255,47	237,13

Personne seule ou couple ayant une
personne à charge	                  326,43 287,46	265,88

Par personne à
charge supplémentaire               47,36  41,84  38,11
--------------------------------------------------------

```catala
champ d'application CalculPlafondLoyer:

  exception base
  définition plafond_loyer_d823_16_2
  sous condition colocation
  conséquence égal à
    selon zone sous forme
    -- Zone1 : 239,48€
    -- Zone2 : 208,71€
    -- Zone3 : 195,62€
```

```catala
déclaration champ d'application Test22:
  résultat calcul contenu CalculPlafondLoyer

champ d'application Test22:
  définition calcul égal à
    résultat de CalculPlafondLoyer avec {
      -- logement_est_chambre: vrai
      -- âgées_ou_handicap_adultes_hébergées_onéreux_particuliers: faux
      -- zone: Zone1
      -- situation_familiale: PersonneSeule
      -- nombre_personnes_à_charge: 0
      -- colocation: vrai
    }
```